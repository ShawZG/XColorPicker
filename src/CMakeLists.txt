SET(CMAKE_CXX_STANDARD 11)

SET(CMAKE_AUTOMOC ON)
SET(CMAKE_AUTORCC ON)
SET(CMAKE_VERBOSE_MAKEFILE ON)

SET(APP_NAME ${PROJECT_NAME})
# QSetting需要的配置
SET(ORGANIZATION org.xzg.${APP_NAME})
SET(APPLICATION ${APP_NAME})

FIND_PACKAGE(Qt5 COMPONENTS Core Gui Widgets LinguistTools REQUIRED)

#Application单例库
SET(QAPPLICATION_CLASS QApplication CACHE STRING "Inheritance class for SingleApplication")
ADD_SUBDIRECTORY(${CMAKE_CURRENT_LIST_DIR}/third_party/SingleApplication)
SET(SINGLE_APP SingleApplication::SingleApplication)

CONFIGURE_FILE(${CMAKE_CURRENT_SOURCE_DIR}/common/version.h.in ${CMAKE_CURRENT_SOURCE_DIR}/common/version.h @ONLY)
FILE(GLOB_RECURSE SRC_FILES RELATIVE ${CMAKE_CURRENT_SOURCE_DIR} *.cpp *.h)
#已经使用了SET(CMAKE_AUTORCC ON)，可以不使用QT5_ADD_RESOURCES, qrc文件最终也是被编译为CPP文件。
#QT5_ADD_RESOURCES(QRC_FILES ${CMAKE_CURRENT_SOURCE_DIR}/resource/res.qrc)
SET(QRC_FILES ${CMAKE_CURRENT_SOURCE_DIR}/resource/res.qrc)

#创建翻译需要的ts文件
#翻译文件ts->qm，使用lupdate和lrelease命令直接生成
#QT5_CREATE_TRANSLATION(QM_FILES ${CMAKE_CURRENT_SOURCE_DIR}/resource/simplified_chinese.ts)
#QT5_CREATE_TRANSLATION(QM_FILES ${CMAKE_CURRENT_SOURCE_DIR}/resource/english.ts)

#Windows平台图标
IF(CMAKE_SYSTEM_NAME MATCHES "Windows")
    SET(WINDOWS_RC_FILE ${PROJECT_SOURCE_DIR}/data/app.rc)
ENDIF()

#将编译产生的可执行文件或库文件存放到指定目录下，不包含编译生成的中间文件
#SET(EXECUTABLE_OUTPUT_PATH ${PROJECT_BINARY_DIR}/bin)

ADD_EXECUTABLE(${APP_NAME} "")
TARGET_SOURCES(${APP_NAME} PRIVATE ${SRC_FILES} ${QRC_FILES} ${WINDOWS_RC_FILE})
TARGET_INCLUDE_DIRECTORIES(${APP_NAME} PRIVATE ${CMAKE_CURRENT_SOURCE_DIR}/common)
TARGET_INCLUDE_DIRECTORIES(${APP_NAME} PRIVATE ${CMAKE_CURRENT_SOURCE_DIR}/dialogs)
TARGET_LINK_LIBRARIES(${APP_NAME} Qt5::Core Qt5::Widgets ${SINGLE_APP})

INSTALL(TARGETS ${APP_NAME} RUNTIME DESTINATION /opt/apps/${ORGANIZATION}/)
INSTALL(FILES ${PROJECT_SOURCE_DIR}/data/xcolorpicker.desktop DESTINATION /usr/share/applications)
INSTALL(FILES ${PROJECT_SOURCE_DIR}/data/org.xzg.xcolorpicker.svg DESTINATION /usr/share/icons/hicolor/scalable/apps)

#构建DEB包
SET(CPACK_GENERATOR "DEB")
SET(CPACK_PACKAGE_NAME "org.xzg.xcolorpicker")
SET(CPACK_PACKAGE_VERSION ${CMAKE_PROJECT_VERSION})
#SET(CPACK_DEBIAN_PACKAGE_ARCHITECTURE amd64)
#SET(CPACK_PACKAGE_FILE_NAME "${CPACK_PACKAGE_NAME}-${CPACK_PACKAGE_VERSION}-Linux-${CPACK_DEBIAN_PACKAGE_ARCHITECTURE}")
SET(CPACK_PACKAGE_CONTACT "blueshaw@163.com")
SET(CPACK_DEBIAN_PACKAGE_MAINTAINER "Xiao Zhiguo")
SET(CPACK_DEBIAN_PACKAGE_DESCRIPTION "A simple app for color picker")
SET(CPACK_DEBIAN_PACKAGE_PRIORITY "Optional")
SET(CPACK_DEBIAN_PACKAGE_DEPENDS "libqt5core5a, libqt5widgets5, libqt5gui5")
INCLUDE(CPack)
